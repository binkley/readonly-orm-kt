buildscript {
    ext.kotlin_version = '1.2-M1'

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        maven { url 'https://plugins.gradle.org/m2' }
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin',
                version: "$kotlin_version"
        classpath group: 'io.gitlab.arturbosch.detekt',
                name: 'detekt-gradle-plugin', version: '1.0.0.M13.2'
    }
}

group = 'hm.binkley.labs'
version = '0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations {
    ktlint
}

dependencies {
    ktlint group: 'com.github.shyiko', name: 'ktlint', version: '0.9.0'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
    compile group: 'org.jetbrains.exposed', name: 'exposed', version: '0.8.4'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit',
            version: '2.0.0.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
}

jacoco {
    toolVersion = '0.7.10-SNAPSHOT'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.67
            }
        }
    }
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.mustRunAfter jacocoTestReport

check.dependsOn detektCheck
check.dependsOn ktlint
