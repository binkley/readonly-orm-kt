buildscript {
    ext {
        versions_version = '0.17.0'
        kotlin_version = '1.2.21'
        detekt_version = '1.0.0.RC6-2'
        hamcrest_version = '2.0.0.0'
        jacoco_version = '0.8.0'
        kassava_version = '1.0.0'
        ktlint_version = '0.15.0'
        mockito_version = '2.13.0'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2' }
    }

    dependencies {
        classpath group: 'com.github.ben-manes',
                name: 'gradle-versions-plugin', version: "$versions_version"
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin',
                version: "$kotlin_version"
        classpath group: 'io.gitlab.arturbosch.detekt',
                name: 'detekt-gradle-plugin', version: "$detekt_version"
    }
}

group = 'hm.binkley.labs'
version = '0-SNAPSHOT'

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'build-dashboard'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations {
    ktlint
}

dependencies {
    ktlint group: 'com.github.shyiko', name: 'ktlint',
            version: "$ktlint_version"
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8',
            version: "$kotlin_version"
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect',
            version: "$kotlin_version"
    compile group: 'au.com.console', name: 'kassava',
            version: "$kassava_version"
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit',
            version: "$hamcrest_version"
    testCompile group: 'org.mockito', name: 'mockito-core',
            version: "$mockito_version"
}

clean {
    delete 'out'
}

detekt {
    profile('main') {
        config = "$projectDir/config/detekt/detekt.yml"
        input = "$projectDir/src/main/kotlin"
        output = "$projectDir/build/reports/detekt/report.xml"
    }
}

task detektReport {
    dependsOn detektCheck
    doLast {
        ant.xslt(in: "$projectDir/build/reports/detekt/report.xml/detekt-checkstyle.xml",
                out: "$projectDir/build/reports/detekt/index.html",
                style: "$projectDir/config/checkstyle/checkstyle-noframes-severity-sorted.xsl") {
        }
    }
}

jacoco {
    toolVersion = "$jacoco_version"
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.71
            }
        }
    }
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['src/**/*.kt']
}

tasks.withType(FindBugs) {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

check {
    dependsOn jacocoTestReport
    dependsOn detektReport
    dependsOn ktlint
}
